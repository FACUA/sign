buildscript {
    ext.kotlin_version = '1.2.41'
    ext.itext_version = '7.1.1'
    ext.bouncycastle_version = '1.59'
    ext.slf4j_version = '1.7.25'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.3"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group 'org.facua'
version '1.4.0'

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'kotlin'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // RxKotlinFx stack
    compile "com.github.thomasnield:rxkotlinfx:2.2.2"
    compile "no.tornado:tornadofx:1.7.14"
    compile "no.tornado:tornadofx-controls:1.0.6"

    // Ghost4J - Needed to render PDFs
    compile "org.ghost4j:ghost4j:1.0.1"

    // iText - Needed to sign PDFs
    compile "com.itextpdf:kernel:$itext_version"
    compile "com.itextpdf:io:$itext_version"
    compile "com.itextpdf:layout:$itext_version"
    compile "com.itextpdf:sign:$itext_version"

    // Cryptographic libraries required by iText
    compile "org.bouncycastle:bcprov-jdk15on:$bouncycastle_version"
    compile "org.bouncycastle:bcpkix-jdk15on:$bouncycastle_version"
    compile "org.bouncycastle:bcmail-jdk15on:$bouncycastle_version"

    // Logger included with iText and Ghost4J
    compile "org.slf4j:slf4j-simple:$slf4j_version"

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    baseName = project.name + '-prod'
    classifier = null
    version = version

    jar {
        manifest {
            attributes 'Implementation-Title': 'Facua Sign',
                    'Implementation-Version': version,
                    'Main-Class': 'MainKt'
        }
    }
}
